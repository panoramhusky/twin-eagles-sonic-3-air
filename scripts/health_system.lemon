global u8 TESonic.health = 3
global u8 TESonic.maxhealth = 3

//# address-hook(0x010294) end(0x010364)
function void Character.GettingHurt()
{
	if (TESonic.isTESonic(A0))
	{
		bool canLoseShield = true	// Usually true, gets false e.g. for 2nd player Tails
		bool isProtectedByObject = ((char.protection_flag & 0x80) != 0)
		bool giveKnockback = isProtectedByObject

		if (canLoseShield && (char.bonus_effect & char.bonus.ANY_SHIELD) == 0)
		{
			if (!isProtectedByObject)
			{
				--TESonic.health
				// No more health?
				if (TESonic.health == 0)
				{
					Character.Die_internal()
					return
				}

				// Should be unnecessary, but make sure all shield flags are cleared
				char.bonus_effect &= ~char.bonus.ALL_SHIELDS
			}
		}
		else
		{
		#if STANDALONE
			// When using e.g. the MGZ spinning top, do not lose your shield
			if (!isProtectedByObject)
		#endif
			{
				// Lose shield
				char.bonus_effect &= ~char.bonus.ALL_SHIELDS
			}
		}
		
		// force him off of whatever he's holding on to
		if (char.control_flags & control_flag.DISABLE_UPDATE)
			giveKnockback = true
		
		if (giveKnockback)
		{
			char.base_state = base_state.HURT
			Character.LandingOnGroundNoSpindash()
			char.flags |= char.flag.IN_AIR
			if (char.flags & char.flag.UNDERWATER)
			{
				char.velocity.x = -0x100
				char.velocity.y = -0x200
			}
			else
			{
				char.velocity.x = -0x200
				char.velocity.y = -0x400
			}
			if (char.position.x.u16 >= u16[A2 + 0x10])
			{
				char.velocity.x = -char.velocity.x
			}

			char.groundspeed = 0
			char.state = char.state.GOT_HURT_PANIC
		}

		playSound(0x35)
		char.invuln.countdown = 120
		
		D0 = 0xffffffff
	}
	else
	{
		base.Character.GettingHurt()
	}
}

//# address-hook(0x00db44) end(0x00dbb2)
function void RenderHUD()
{
	base.RenderHUD()

	A1 = 0xffffb000
	if (TESonic.isTESonic(A1))
	{
	#if STANDALONE
		if (DEBUG_DISABLE_HUD)
			return
	#endif

		s16 baseX = global.level_started
		baseX += 0x0f
		s16 baseY = 0x08
		u16 renderQueue = 0xe000
		
		if (!debug_mode.state)
		{
			if (TESonic.health > TESonic.maxhealth)
				TESonic.health = TESonic.maxhealth
		
			baseY += 174
			for (u8 i == 0; i < TESonic.maxhealth; ++i)
			{		
				u16 offset = (i * 16)
			
				string key
				
				if (i < TESonic.health)
				{
					if (super.active)
						key = "hud_health_icon_super"
					else
						key = "hud_health_icon_full"
				}
				else
					key = "hud_health_icon_empty"
				
				TESonic.HUD.drawHeart(key, baseX + offset, baseY, renderQueue)
			}
		}
	}
}

function void TESonic.HUD.drawHeart(u64 key, u16 px, u16 py, u16 renderQueue)
{
	SpriteHandle heart = Renderer.addSpriteHandle(key, px, py, renderQueue)
	
	heart.setPriorityFlag(true)
	
	if (super.active)
	{
		u32 color = convertColors32(unpackColor(u32[0x802002]))
		//debugLog(color)
		
		u8 r = u8(color >> 24)
		u8 g = u8(color >> 16)
		u8 b = u8(color >> 8)
		
		float r2 = r / 255.0
		float g2 = g / 255.0
		float b2 = b / 255.0
		
		heart.setAddedColor(r2 / 1.2, g2 / 1.2, b2 / 1.2)
	}
}